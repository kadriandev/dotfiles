#!/usr/bin/env zsh

# Disable specific apps
yabai -m rule --add app="^System Settings$" manage=off
yabai -m rule --add app="^System Information$" manage=off
yabai -m rule --add app="^Calculator$" manage=off
yabai -m rule --add app="^Pulse Secure$" manage=off
yabai -m rule --add app="^Messages$" manage=off
yabai -m rule --add app="^Finder$" manage=off
yabai -m rule --add app="^Raycast$" manage=off
yabai -m rule --add app="^Microsoft Teams (work or school)$" title="^Microsoft Teams Notification$" manage=off

yabai -m signal --add \
      event=application_launched \
      app="^Microsoft Teams (work or school)$" \
      action='${HOME}/.config/yabai/scripts/defeat-teams.sh'

yabai -m signal --add \
      event=window_focused \
      app="^Microsoft Teams (work or school)$" \
      title="^Microsoft Teams Notification$" \
      action='${HOME}/.config/yabai/scripts/defeat-teams.sh'

declare -r YABAIRC=$0

function get_display_aspect_ratio {
  case $(yabai -m query --displays --display mouse | jq '.frame | (.w)/(.h)*1.2 | round') in
    (2) echo "16:9" ;;
    (3) echo "32:9" ;;
    (*) echo "something:whacky" ;;
  esac
}

function get_space_num_visible_windows {
 yabai -m query --windows --space mouse | \
   jq 'map(select(.visible == 1 and .floating == 0)) |
    # deal with how hidden applications apparently still
    # have "visible" windows
    if all(.split == "none") then
      1
    else
      map(select(.split != "none")) | length
    end'
}

function maybe_center_space {
  if [[ $(get_display_aspect_ratio) == "32:9" ]]; then
    case $(get_space_num_visible_windows) in
      (<0-1>) yabai -m space --padding abs:40:10:400:400 ;;
      (<2-4>) yabai -m space --padding abs:40:10:100:100 ;;
      (*)     yabai -m space --padding abs:40:10:10:10   ;;
    esac
  else
    yabai -m space --padding abs:20:10:10:10
  fi
}

function config_signals {
  for signal (
    application_launched application_terminated
    application_hidden application_visible
    window_created window_destroyed window_minimized window_deminimized
    space_changed display_resized
  ); do
    yabai -m signal --add event=$signal \
      action="$YABAIRC maybe_center_space" \
      label="maybe_center_space-on-$signal"
  done
    yabai -m signal --add event=window_focused action="sketchybar --trigger window_focus"
    yabai -m signal --add event=window_created action="sketchybar --trigger windows_on_spaces"
    yabai -m signal --add event=window_destroyed action="sketchybar --trigger windows_on_spaces"
}

function config_main {
    yabai -m signal --add event=dock_did_restart action="sudo yabai --load-sa"
    sudo yabai --load-sa

    yabai -m config layout                       bsp
    yabai -m config window_gap                   06
    yabai -m config mouse_follows_focus on
    yabai -m config window_placement second_child
    yabai -m config window_shadow float
    
    yabai -m config window_opacity on
    yabai -m config active_window_opacity 1.0
    yabai -m config normal_window_opacity 0.7

    # padding
    yabai -m config external_bar all:10:10
    yabai -m config bottom_padding 12
    yabai -m config window_gap 12

    maybe_center_space
    config_signals
}

if [[ $# == 0 ]]; then
  config_main
else
  $1
fi

